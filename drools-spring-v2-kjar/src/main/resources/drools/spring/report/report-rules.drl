//created on: Aug 24, 2018
package report

//list any import classes here.
import com.sbnz.doctor.dto.DiagnosisDTO;
import com.sbnz.doctor.dto.PatientDTO;
import com.sbnz.doctor.dto.DiseaseDTO;
import com.sbnz.doctor.dto.MedicineallergyDTO;
import com.sbnz.doctor.dto.AllergyDTO;
import com.sbnz.doctor.dto.ReportDiagnosis;
import com.sbnz.doctor.dto.TherapyReport;
import com.sbnz.doctor.model.Patient;
import com.sbnz.doctor.model.Diagnosis;
import com.sbnz.doctor.model.Symptom;
import com.sbnz.doctor.model.Disease;
import com.sbnz.doctor.services.DiseaseService;
import com.sbnz.doctor.services.SymptomService;
import com.sbnz.doctor.services.PatientService;
import com.sbnz.doctor.utils.SymptomList;
import com.sbnz.doctor.utils.MedicineList;
import com.sbnz.doctor.utils.CheckAllergyObject;
import com.sbnz.doctor.utils.ReportEntity;
import java.util.List;
import java.util.HashMap;
import java.util.Set;


//declare any global variables here
global java.util.Date immunityLimit; 

rule "Chronic disease patients"
	lock-on-active true
	no-loop true
	agenda-group "Chronic"
    when
        //conditions
        $re : ReportEntity($diags : diagnoses , $bolesti : diseases , $pacijenti : patients  )
		$dto: PatientDTO( $pat : patientId  ) from $pacijenti
        Number( $sum : intValue > 5) from accumulate(
        	DiseaseDTO( $id : diseaseId ) from $bolesti and
        	ReportDiagnosis( $id == diseaseId && diseaseCode != "PREHL" && diseaseCode != "GROZN" && $pat == patientId) from $diags,
        	init(int count = 0;),
        	action(count++;),
        	result(count)
        )
    then
        //actions
        $re.getRetVal().add($dto.getPatientId());

end

rule "Potential addicts"
    lock-on-active true
	no-loop true
	agenda-group "Addicts"
    when
        //conditions
        $re : ReportEntity($pacijenti : patients , $terapije : therapies  )
		$dto: PatientDTO( $pat : patientId  ) from $pacijenti
		Number ($sum : intValue >= 6) from accumulate(
			$ter: TherapyReport( medicineType == 'P', $lekar : userId, patientId == $pat ) from $terapije,
			init ( int count = 0; ),
			action ( count++; $re.getLekari().add($lekar);),
			result ( count )
		)
    then
        //actions
        if($re.getLekari().size() >= 3){
        	$re.getRetVal().add($pat);
        }

end

rule "Weak immunity"
    lock-on-active true
	no-loop true
	agenda-group "Immunity"
    when
        //conditions
        $re : ReportEntity($pacijenti : patients , $terapije : therapies  )
		$dto: PatientDTO( $pat : patientId  ) from $pacijenti
		Number ($sum : intValue > 10) from accumulate(
			$ter: TherapyReport(  patientId == $pat, $tip : medicineType, $bolest : diseaseCode, therapyDate.getTime() >= immunityLimit.getTime() ) from $terapije,
			init ( int count = 0; ),
			action ( count++; $re.getBolesti().add($bolest); $re.getTipovi().add($ter.getMedicineType());),
			result ( count )
		) and Number($sum2 : intValue >= 0) from accumulate(
			$ter: TherapyReport( medicineType != 'A', patientId == $pat, therapyDate.getTime() >= immunityLimit.getTime() ) from $terapije,
			init ( int count = 0; ),
			action ( count++; ),
			result ( count )
		)
    then
        //actions
        if($re.getBolesti().size() >= 2 && $sum2 == 0){
        	$re.getRetVal().add($pat);
        }
		
end